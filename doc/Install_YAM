;
; $VER: Installer script 1.7 for YAM 2.5 (XX.12.2007) © 2000-2007 by YAM Open Source Team
; Language support added by Sönke Tesch/ATO
;
; $Id$
;
;
;
; /// Setting up
(set #def_lang_English              0)
(set #def_lang_English_British      1)
(set #def_lang_Deutsch              2)
(set #def_lang_Francais             4)
(set #def_lang_Svenska              8)

(set #def_doc_English               0)
(set #def_doc_Deutsch               1)
(set #def_doc_Francais              2)

; ///
;----------------------------------------------------------------------------
;/// ***** English
(set #def_cat_lang #def_lang_English)
(set #def_doc_lang #def_doc_English)
(set #wrongOS  "YAM needs at least OS 3.0 to run!\n")
(set #wrongMUI "YAM needs at least MUI 3.8 to run!\n")
(set #wrongCPU "YAM needs at least a 68020 CPU to run!\n")
(set #YAM_dir  (cat "Please select a directory for YAM\n"
                    "(NO extra drawer will be created!)"))
(set #lang_catalog (cat "Which catalog file do you want to install?\n"
                        "(English language is built-in)"))
(set #lang_manual "Select language for online AmigaGuide help")
(set #assign_prompt (cat "A \"YAM:\" assign needs to be added to your "
                         "\"S:User-Startup\" file.\n\n"
                         "Do you mind?"))
(set #assign_help (cat "If this assign is not present, the next time you boot up "
                       "your machine the program will be unable to find the files "
                       "and drawers it needs to run.\n"))
(set #done_1   (cat "\nYAM has been successfully installed.\n\n"
                    "Double click the \"YAM\" icon in the\n"))
(set #done_2        "\ndirectory to taste it.\n\nHave fun...")

; ///
; /// ***** Deutsch
(IF (= @language "deutsch")
(
(set #def_cat_lang #def_lang_Deutsch)
(set #def_doc_lang #def_doc_Deutsch)
(set #wrongOS  "YAM benötigt OS3.0 oder höher\n")
(set #wrongMUI "YAM benötigt MUI3.8 oder höher\n")
(set #wrongCPU "YAM läuft nur ab einer 68020 CPU\n")
(set #YAM_dir  (cat"Wählen Sie ein Verzeichnis für YAM\n"
                   "(es wird KEIN eigenes Verzeichnis erstellt!)"))
(set #lang_catalog (cat "Welche catalog-Datei wollen Sie installieren?\n"
                        "(Englisch ist eingebaut)"))
(set #lang_manual "Wählen Sie eine Sprache für die Online-AmigaGuide-Hilfe")
(set #assign_prompt (cat "In S:User-Startup sollte ein Assign YAM: eingefügt werden.\n"
                         "Sind Sie damit einverstanden?"))
(set #assign_help (cat "Wenn Sie vom Installer gefragt werden, ob er diese Zuweisung "
                       "in der Datei s:user-startup vornehmen darf, sollten Sie den "
                       "Knopf \"Weiter\" drücken; andernfalls kann das Programm nach "
                       "dem nächsten Booten die benötigten Dateien und Verzeichnisse "
                       "nicht finden.\n"))
(set #done_1   (cat "\nYAM wurde erfolgreich installiert.\n\n"
                    "Klicken Sie auf das \"YAM\"-Icon im Verzeichnis\n"))
(set #done_2        "\nund es kann losgehen.\n\nViel Spass!")
)
)
; ///
; /// ***** Français
(IF (= @language "français")
(
(set #def_cat_lang #def_lang_Francais)
(set #def_doc_lang #def_doc_Francais)
(set #wrongOS  "YAM a besoin de l' OS 3.0 minimum !\n")
(set #wrongMUI "YAM a besoin de MUI 3.8 minimum !\n")
(set #wrongCPU "YAM a besoin d'un CPU 68020 minimum !\n")
(set #YAM_dir  (cat "Choisissez le répertoire d'installation pour YAM.\n"
                    "(Il n'y aura pas de création de répertoire !)"))
(set #lang_catalog (cat "Quel langage désirez-vous installer ?\n"
                        "(L'anglais est intégré au programme)"))
(set #lang_manual "Choisissez la langue de l'aide en ligne. (AmigaGuide)")
(set #assign_prompt (cat "l'assignation « YAM: » devrait être ajoutée à "
                         "« S:User-Startup ».\n"
                         "Le souhaitez-vous ?"))
(set #assign_help (cat "YAM a besoin d'une assignation pour fonctionner correctement. "
                       "Si cette assignation n'est pas présente, la prochaine fois "
                       "que vous démarrerez l'Amiga, le programme ne sera pas capable "
                       "de trouver les fichiers et les répertoires nécessaires "
                       "à son exécution.\n"))
(set #done_1   (cat "\nYAM a été installé avec succès.\n\n"
                    "Cliquez sur l'icône de « YAM » dans le répertoire\n"))
(set #done_2        "\npour l'essayer.\n\nAmusez vous bien !!")
)
)
; ///
; /// ***** Svenska
(IF (= @language "svenska")
(
(set #def_cat_lang #def_lang_Svenska)
(set #wrongOS  "YAM kräver OS 3.0 eller senare\n")
(set #wrongMUI "YAM kräver MUI 3.8 eller senare\n")
(set #wrongCPU "YAM kräver 68020 processor eller bättre\n")
(set #YAM_dir  (cat "Välj katalog att installera YAM i\n"
                    "(någon underkatalog kommer INTE att skapas!)"))
(set #lang_catalog (cat "Vilka språkkataloger vill du installera?\n"
                        "(Engelska är inbyggt)"))
(set #lang_manual "Välj språk för den AmigaGuide-baserade hjälpen")
(set #assign_prompt (cat "\"Assign YAM:\" behöver adderas till S:User-Startup\n"
                         "Är det Ok?"))
(set #assign_help (cat "När installationsprogrammet frågar efter att göra denna "
                       "tilldelning i filen s:user-startup, bör du klicka på knappen "
                       "\"Fortsätt\". Annars kan YAM inte hitta sina filer "
                       "och kataloger efter nästa ombootning\n"))
(set #done_1   (cat "\nYAM installerades utan problem\n\n"
                    "Klicka på \"YAM\"-ikonen i katalogen\n"))
(set #done_2        "\nför att pröva det.\n\nHa det så bra!")
)
)
; ///
;----------------------------------------------------------------------------
; /// Checking environment
;(set installer_ver (/ @installer-version 65536))
;(set installer_rev (- @installer-version (* installer_ver 65536)))
;(if (or (< installer_ver 42) (< installer_rev 6)) (abort #wronginstaller))

(set OS_ver (getversion "exec.library" (resident)))
(if (< OS_ver (* 39 65536)) (abort #wrongOS))

(set MUI_ver (getversion "MUI:libs/muimaster.library"))
(if (and (< MUI_ver (* 19 65536)) (<> MUI_ver 0)) (abort #wrongMUI))

(set CPU_type (database "cpu"))
(if (< CPU_type "68020") (abort #wrongCPU))

; we use newlib.library to check whether we are running on OS4 or MorphOS
(set NEWLIB_ver (getversion "newlib.library" (resident)))
(if (>= NEWLIB_ver (* 4 65536))
  (set os4 1)
  (set os4 0)
)

(if os4
  ; OS4 does use english locale names only
  (
    (set #cat_name_English_British       "english-british")
    (set #cat_name_Deutsch               "german")
    (set #cat_name_Francais              "french")
    (set #cat_name_Svenska               "swedish")
  )
  ; OS3 and MOS use localized locale names
  (
    (set #cat_name_English_British       "english-british")
    (set #cat_name_Deutsch               "deutsch")
    (set #cat_name_Francais              "francais")
    (set #cat_name_Svenska               "svenska")
  )
)

; ///
; /// Let's ask user for anything
(complete 0)

(if (<> 0 (exists "YAM:" (noreq)))
   (set YAM_def_dir (getassign "YAM"))
   (set YAM_def_dir @default-dest)
)
(set YAM_dir
   (askdir
      (default YAM_def_dir)
      (prompt  #YAM_dir)
      (help    @askdir-help)
   )
)

(complete 10)

(set Catalog_lang
   (askoptions
      (prompt  #lang_catalog)
      (help @askoptions-help)
      (choices "English (british)"
               "Deutsch"
               "Français"
               "Svenska"
      )
      (default #def_cat_lang)
   )
)

(complete 15)

(set Docs_lang
   (askchoice
      (prompt  #lang_manual)
      (help    @askchoice-help)
      (choices "English (V2.5)"
               "Deutsch (V2.4)"
               "Français (V2.4)"
      )
      (default #def_doc_lang)
   )
)

(complete 20)
; ///
; /// Here we go!
(complete 30)

(makedir YAM_dir (infos))
(if (not (exists (tackon YAM_dir "YAM.info")))
   (copyfiles (source "/YAM.info")
              (dest YAM_dir)
              (infos)
              (noposition)
              (help @copyfiles-help)
   )
)
(copyfiles (source "/YAM") (dest YAM_dir) (help @copyfiles-help))

(complete 40)

(if (not (exists (tackon YAM_dir ".taglines")))
   (copyfiles (source "/.taglines") (dest YAM_dir) (help @copyfiles-help))
)

(if (> Catalog_lang 0)
   (
      (set Cat_dir (tackon YAM_dir "Catalogs"))
      (if (not (exists Cat_dir))  (makedir Cat_dir))

      (if (bitand Catalog_lang #def_lang_English_British)
         (
            (set lang #cat_name_English_British)
            (copyfiles (source (tackon "/Catalogs" lang))
               (choices "YAM.catalog")
               (dest (tackon Cat_dir lang))
            )
         )
      )

      (if (bitand Catalog_lang #def_lang_Deutsch)
         (
            (set lang #cat_name_Deutsch)
            (copyfiles (source (tackon "/Catalogs" lang))
               (choices "YAM.catalog")
               (dest (tackon Cat_dir lang))
            )
         )
      )

      (if (bitand Catalog_lang #def_lang_Francais)
         (
            (set lang #cat_name_Francais)
            (copyfiles (source (tackon "/Catalogs" lang))
               (choices "YAM.catalog")
               (dest (tackon Cat_dir lang))
            )
         )
      )

      (if (bitand Catalog_lang #def_lang_Svenska)
         (
            (set lang #cat_name_Svenska)
            (copyfiles (source (tackon "/Catalogs" lang))
               (choices "YAM.catalog")
               (dest (tackon Cat_dir lang))
            )
         )
      )
   )
)

(complete 50)

(if (= Docs_lang #def_doc_English)
   (copyfiles (source "/Docs/YAM_english.guide")
      (dest YAM_dir)
      (newname "YAM.guide")
      (infos)
      (noposition)
   )
)
(if (= Docs_lang #def_doc_Deutsch)
   (copyfiles (source "/Docs/YAM_deutsch.guide")
      (dest YAM_dir)
      (newname "YAM.guide")
      (infos)
      (noposition)
   )
)
(if (= Docs_lang #def_doc_Francais)
   (copyfiles (source "/Docs/YAM_français.guide")
      (dest YAM_dir)
      (newname "YAM.guide")
      (infos)
      (noposition)
   )
)

(copyfiles (source "/Readme First") (dest YAM_dir) (infos) (noposition))
(copyfiles (source "/GPL.txt")      (dest YAM_dir) (noposition))

(complete 60)

(set Themes_dir (tackon YAM_dir "Themes"))
(if (not (exists Themes_dir)) (makedir Themes_dir))
(copyfiles (source "/Themes/")
           (dest Themes_dir)
           (all)
)
(copyfiles (source "/Themes.info") (dest YAM_dir) (noposition))

(complete 70)

(set ARexx_dir (tackon YAM_dir "Rexx"))
(if (not (exists ARexx_dir)) (makedir ARexx_dir))
(copyfiles (source "/Rexx/")
           (dest ARexx_dir)
           (all)
)
(copyfiles (source "/Rexx.info") (dest YAM_dir) (noposition))

(complete 80)

(foreach "MUI/" "#?.m??"
   (copylib (prompt (cat "MUI - " @each-name))
            (source (cat "MUI/" @each-name))
            (dest "MUI:Libs/MUI/")
            (confirm)
            (help @copylib-help)
   )
)

(complete 85)

(foreach "Libs/" "#?.library"
   (copylib (prompt @each-name)
            (source (cat "Libs/" @each-name))
            (dest "LIBS:")
            (confirm)
            (help @copylib-help)
   )
)

(complete 90)

(startup "YAM - Amiga mailer © Marcel Beck & YAM Open Source Team"
   (prompt #assign_prompt)
   (help   #assign_help)
   (command "C:Assign YAM: \"" YAM_dir "\"\n")
)
(makeassign "YAM" YAM_dir)

(complete 100)

(message #done_1 "\"" YAM_dir "\"" #done_2)

; /// ********************
;----------------------------------------------------------------------------
(exit (QUIET))

