;
; $VER: Installer script 1.7 for YAM 2.5 (21.12.2007) (c) 2000-2007 by YAM Open Source Team
; Language support added by Sönke Tesch/ATO
;
; $Id$
;
;
;
; /// Setting up
(set #def_lang_English              0)
(set #def_lang_Deutsch              1)
(set #def_lang_Svenska              2)
; accents of languages should be appended at the end rather than somewhere in the middle,
; because only OS4 can handle them.
(set #def_lang_English_British      4)

(set #def_doc_English               0)

; ///
;----------------------------------------------------------------------------
;/// ***** English
(set #def_cat_lang #def_lang_English)
(set #def_doc_lang #def_doc_English)
(set #wrongOS  "YAM needs at least OS 3.0 to run!\n")
(set #wrongMUI "YAM needs at least MUI 3.8 to run!\n")
(set #wrongCPU "YAM needs at least a 68020 CPU to run!\n")
(set #YAM_dir  (cat "Please select a directory for YAM\n"
                    "(NO extra drawer will be created!)"))
(set #lang_catalog (cat "Which catalog file do you want to install?\n"
                        "(English language is built-in)"))
(set #lang_manual "Select language for online AmigaGuide help")
(set #faq_and_bugtracker (cat "\n"
                              "If you have any questions concerning YAM\n"
                              "you can ask these in the YAM forum:\n"
                              "http://forum.yam.ch/\n"
                              "\n"
                              "Frequently asked questions are already\n"
                              "answered in the YAM FAQ:\n"
                              "http://faq.yam.ch/\n"
                              "\n"
                              "In case you find a bug in YAM, please,\n"
                              "report it in the YAM bugtracker:\n"
                              "http://bugs.yam.ch/"))
(set #done_1   (cat "\nYAM has been successfully installed.\n\n"
                    "Double click the \"YAM\" icon in the\n"))
(set #done_2        "\ndirectory to taste it.\n\nHave fun...")

; ///
; /// ***** Deutsch
(IF (= @language "deutsch")
(
(set #def_cat_lang #def_lang_Deutsch)
(set #wrongOS  "YAM benötigt OS3.0 oder höher\n")
(set #wrongMUI "YAM benötigt MUI3.8 oder höher\n")
(set #wrongCPU "YAM läuft nur ab einer 68020 CPU\n")
(set #YAM_dir  (cat"Wählen Sie ein Verzeichnis für YAM\n"
                   "(es wird KEIN eigenes Verzeichnis erstellt!)"))
(set #lang_catalog (cat "Welche catalog-Datei wollen Sie installieren?\n"
                        "(Englisch ist eingebaut)"))
(set #lang_manual "Wählen Sie eine Sprache für die Online-AmigaGuide-Hilfe")
(set #faq_and_bugtracker (cat "\n"
                              "Falls Sie allgemeine Fragen zu YAM haben,\n"
                              "können Sie diese im YAM-Forum stellen:\n"
                              "http://forum.yam.ch/\n"
                              "\n"
                              "Oft gestellte Fragen werden in der YAM-FAQ\n"
                              "beantwortet:\n"
                              "http://faq.yam.ch/\n"
                              "\n"
                              "Falls Sie einen Fehler in YAM finden sollten,\n"
                              "dann melden Sie ihn bitte im YAM-Bugtracker:\n"
                              "http://bugs.yam.ch/"))
(set #done_1   (cat "\nYAM wurde erfolgreich installiert.\n\n"
                    "Klicken Sie auf das \"YAM\"-Icon im Verzeichnis\n"))
(set #done_2        "\nund es kann losgehen.\n\nViel Spass!")
)
)
; ///
; /// ***** Svenska
(IF (= @language "svenska")
(
(set #def_cat_lang #def_lang_Svenska)
(set #wrongOS  "YAM kräver OS 3.0 eller senare\n")
(set #wrongMUI "YAM kräver MUI 3.8 eller senare\n")
(set #wrongCPU "YAM kräver 68020 processor eller bättre\n")
(set #YAM_dir  (cat "Välj katalog att installera YAM i\n"
                    "(någon underkatalog kommer INTE att skapas!)"))
(set #lang_catalog (cat "Vilka språkkataloger vill du installera?\n"
                        "(Engelska är inbyggt)"))
(set #lang_manual "Välj språk för den AmigaGuide-baserade hjälpen")
(set #faq_and_bugtracker (cat "\n"
                              "If you have any questions concerning YAM\n"
                              "you can ask these in the YAM forum:\n"
                              "http://forum.yam.ch/\n"
                              "\n"
                              "Frequently asked questions are already\n"
                              "answered in the YAM FAQ:\n"
                              "http://faq.yam.ch/\n"
                              "\n"
                              "In case you find a bug in YAM, please,\n"
                              "report it in the YAM bugtracker:\n"
                              "http://bugs.yam.ch/"))
(set #done_1   (cat "\nYAM installerades utan problem\n\n"
                    "Klicka på \"YAM\"-ikonen i katalogen\n"))
(set #done_2        "\nför att pröva det.\n\nHa det så bra!")
)
)
; ///
;----------------------------------------------------------------------------
; /// Checking environment
;(set installer_ver (/ @installer-version 65536))
;(set installer_rev (- @installer-version (* installer_ver 65536)))
;(if (or (< installer_ver 42) (< installer_rev 6)) (abort #wronginstaller))

(set OS_ver (getversion "exec.library" (resident)))
(if (< OS_ver (* 39 65536)) (abort #wrongOS))

(set MUI_ver (getversion "MUI:libs/muimaster.library"))
(if (and (< MUI_ver (* 19 65536)) (<> MUI_ver 0)) (abort #wrongMUI))

(set CPU_type (database "cpu"))
(if (< CPU_type "68020") (abort #wrongCPU))

; we use newlib.library to check whether we are running on OS4 or MorphOS
(set NEWLIB_ver (getversion "newlib.library" (resident)))
(if (>= NEWLIB_ver (* 4 65536))
  (
    (set os3 0)
    (set os4 1)
    (set mos 0)
  )
  (
    (set os4 0)
    (if (>= OS_VER (* 51 65536))
      (
        (set mos 1)
        (set os3 0)
      )
      (
        (set os3 1)
        (set mos 0)
      )
    )
  )
)

(if os4
  ; OS4 does use english locale names only
  (
    (set #cat_name_Deutsch               "german")
    (set #cat_name_Svenska               "swedish")
    (set #cat_name_English_British       "english-british")
  )
  ; OS3 and MOS use localized locale names
  (
    (set #cat_name_Deutsch               "deutsch")
    (set #cat_name_Svenska               "svenska")
  )
)

; ///
; /// Let's ask user for anything
(complete 0)

(if (<> 0 (exists "YAM:" (noreq)))
   (set YAM_def_dir (getassign "YAM"))
   (set YAM_def_dir @default-dest)
)
(set YAM_dir
   (askdir
      (default YAM_def_dir)
      (prompt  #YAM_dir)
      (help    @askdir-help)
   )
)

(complete 10)

(if os4
   ; OS4 can handle different accents of one language (i.e. british english)
   (set Catalog_lang
      (askoptions
         (prompt  #lang_catalog)
         (help @askoptions-help)
         (choices "Deutsch"
                  "Svenska"
                  "English (british)"
         )
         (default #def_cat_lang)
      )
   )
   ; but OS3/MOS cannot
   (set Catalog_lang
      (askoptions
         (prompt  #lang_catalog)
         (help @askoptions-help)
         (choices "Deutsch"
                  "Svenska"
         )
         (default #def_cat_lang)
      )
   )
)

; disabled temporarily
;(set Docs_lang
;   (askchoice
;      (prompt  #lang_manual)
;      (help    @askchoice-help)
;      (choices "English")
;      (default #def_doc_lang)
;   )
;)

(complete 20)
; ///
; /// Here we go!
(complete 30)

; make sure the YAM dir really exists
(makedir YAM_dir (infos))

; copy the executable
(copyfiles (source "/YAM") (dest YAM_dir) (help @copyfiles-help))
(if (not (exists (tackon YAM_dir "YAM.info")))
   (copyfiles (source "/YAM.info")
              (dest YAM_dir)
              (infos)
              (noposition)
              (help @copyfiles-help)
   )
)

(complete 40)

(if (not (exists (tackon YAM_dir ".taglines")))
   (copyfiles (source "/.taglines") (dest YAM_dir) (help @copyfiles-help))
)

(if (not (exists (tackon YAM_dir ".addressbook")))
   (copyfiles (source "/.addressbook") (dest YAM_dir) (help @copyfiles-help))
)

(if (> Catalog_lang 0)
   (
      (set Cat_dir (tackon YAM_dir "Catalogs"))
      (if (not (exists Cat_dir))  (makedir Cat_dir))


      (if (not (exists (tackon YAM_dir "Catalogs.info")))
         (copyfiles (source "/Catalogs.info")
                    (dest YAM_dir)
                    (infos)
                    (noposition)
                    (help @copyfiles-help)
         )
      )

      (if (bitand Catalog_lang #def_lang_Deutsch)
         (
            (makedir (tackon Cat_dir #cat_name_Deutsch))

            (copyfiles (source "/Catalogs/german")
               (choices "YAM.catalog")
               (dest (tackon Cat_dir #cat_name_Deutsch))
            )
         )
      )

      (if (bitand Catalog_lang #def_lang_Svenska)
         (
            (makedir (tackon Cat_dir #cat_name_Svenska))

            (copyfiles (source "/Catalogs/swedish")
               (choices "YAM.catalog")
               (dest (tackon Cat_dir #cat_name_Svenska))
            )
         )
      )

      ; accents of certain languages (OS4 only)
      (if (bitand Catalog_lang #def_lang_English_British)
         (
            (makedir (tackon Cat_dir #cat_name_English_British))

            (copyfiles (source "/Catalogs/english-british")
               (choices "YAM.catalog")
               (dest (tackon Cat_dir #cat_name_English_British))
            )
         )
      )
   )
)

; disabled temporarily
;(if (= Docs_lang #def_doc_English)
   (copyfiles (source "/Docs/YAM_english.guide")
      (dest YAM_dir)
      (newname "YAM.guide")
      (infos)
      (noposition)
   )
;)

(complete 50)

(set Gallery_dir (tackon YAM_dir "Gallery"))
(if (not (exists Gallery_dir)) (makedir Gallery_dir))
(copyfiles (source "/Gallery/")
           (dest Gallery_dir)
           (all)
)

(if (not (exists (tackon YAM_dir "Gallery.info")))
   (copyfiles (source "/Gallery.info")
              (dest YAM_dir)
              (infos)
              (noposition)
              (help @copyfiles-help)
   )
)

(set Docs_dir (tackon YAM_dir "Docs"))
(if (not (exists Docs_dir)) (makedir Docs_dir))
(copyfiles (source "/Docs/")
           (dest Docs_dir)
           (all)
)

(if (not (exists (tackon YAM_dir "Docs.info")))
   (copyfiles (source "/Docs.info")
              (dest YAM_dir)
              (infos)
              (noposition)
              (help @copyfiles-help)
   )
)

(copyfiles (source "/ReadMe")  (dest YAM_dir) (infos))

(complete 60)

(set Themes_dir (tackon YAM_dir "Themes"))
(if (not (exists Themes_dir)) (makedir Themes_dir))
(copyfiles (source "/Themes/")
           (dest Themes_dir)
           (all)
)

(if (not (exists (tackon YAM_dir "Themes.info")))
   (copyfiles (source "/Themes.info")
              (dest YAM_dir)
              (infos)
              (noposition)
              (help @copyfiles-help)
   )
)

(complete 70)

(set ARexx_dir (tackon YAM_dir "Rexx"))
(if (not (exists ARexx_dir)) (makedir ARexx_dir))
(copyfiles (source "/Rexx/")
           (dest ARexx_dir)
           (all)
)

(if (not (exists (tackon YAM_dir "Rexx.info")))
   (copyfiles (source "/Rexx.info")
              (dest YAM_dir)
              (infos)
              (noposition)
              (help @copyfiles-help)
   )
)

(complete 80)

(foreach "MUI/" "#?.m??"
   (copylib (prompt (cat "MUI - " @each-name))
            (source (cat "MUI/" @each-name))
            (dest "MUI:Libs/MUI/")
            (confirm)
            (help @copylib-help)
   )
)

(complete 90)

(foreach "Libs/" "#?.library"
   (copylib (prompt @each-name)
            (source (cat "Libs/" @each-name))
            (dest "LIBS:")
            (confirm)
            (help @copylib-help)
   )
)

(if (or os3 mos)
  (copyfiles (source "Libs/Charsets/")
             (dest "LIBS:Charsets")
             (all)
  )
)

(complete 99)

(message #faq_and_bugtracker)

(complete 100)

(message #done_1 "\"" YAM_dir "\"" #done_2)

; /// ********************
;----------------------------------------------------------------------------
(exit (QUIET))

