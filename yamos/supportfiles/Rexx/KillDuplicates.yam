/* KillDuplicates.yam - Remove duplicate messages in current folder        */
/* $VER: KillDuplicates.yam 1.0 (09.01.00) © 2000 by M.Beck <mbeck@yam.ch> */
/* Requires YAM 2.1 or later                                               */
/* Note: Switch off 'List like multiselect' in MUI/NListviews/Misc/Misc6   */

ADDRESS YAM
OPTIONS RESULTS
FOLDERINFO STEM fi.
dups = 0
LISTSELECT 'NONE'
DO i=0 TO fi.total-1
   MAILINFO STEM mi. i
   mails.i.msgid = mi.msgid
   mails.i.isdup = 0
   DO j=0 TO i-1
      IF mails.i.msgid = mails.j.msgid THEN mails.i.isdup = 1
   END
   IF mails.i.isdup = 1 THEN DO
      dups = dups+1
      LISTSELECT i
   END
END
IF dups > 0 THEN DO
   'Request BODY "'dups' duplicate(s) found" GADGETS "Delete|Keep"'
   IF RESULT=1 THEN MAILDELETE FORCE
END
